#!/bin/tcsh -f


################################################################################
# Source a script that is called at the start of each back-end 
# scripts/qa/run* script
################################################################################
source ./runPre -tool_name $0 $argv
if( $status != 0 ) exit $status


#####################################################
# Generate the batch switch conditionally based on
# whether we want to see the GUI.
#####################################################
if (${guimode}) then
   setenv BATCH_SWITCH ""
else
   setenv BATCH_SWITCH "-batch"
endif

setenv CTECH_LIB_NAME ${CTECH_SIM_LIB_NAME}

#####################################################
# UNIX Environment setup
#####################################################
cd ..

unsetenv SB_STDCELLS_HDL

set ace_network=""
# Setup for running Spyglass on the endpoint
if ($config == "IOSF_Sideband_Endpoint") then


   #####################################################
   # Setup env variables for Spyglass accordingly to
   # endpoint configuration
   #####################################################
   source ${sbrdir}/scripts/qa/ephelper.csh -rtlcfg ${component} \
                                             -topdir ${sbrdir} 



   #####################################################
   # Select the input file-list
   #####################################################
   setenv RTL_FILE_LIST ${sbrdir}/tools/spyglassdft/sbe_rtl_list.f


   #####################################################
   # Clean and create the run directory
   #####################################################
   setenv SPYGLASS_RUN_DIR ${sbrdir}/tools/spyglassdft/sbendpoint/${component}
   if (-d ${SPYGLASS_RUN_DIR}) then
       rm -rf ${SPYGLASS_RUN_DIR}
   endif
   mkdir -p ${SPYGLASS_RUN_DIR}



   #####################################################
   # Locate project file
   #####################################################
   cp ${sbrdir}/tools/spyglassdft/sbe_spyglass_dft.prj ${SPYGLASS_RUN_DIR}
   setenv SPYGLASS_PROJ_FILE ${SPYGLASS_RUN_DIR}/sbe_spyglass_dft.prj
   perl -pi -e "s/<inst>/${component}/" ${SPYGLASS_PROJ_FILE}


   #####################################################
   # Make a copy of the upf_wrapper and adjust the default
   # RTL parameters according to the configuration.
   #
   # This is just a wrapper. It is named upf_wrapper
   # only because that was what it was used for first.
   #####################################################
   ${sbrdir}/scripts/qa/setRTLparams.pl \
          -config  ${sbrdir}/source/cfg/endpoint/csv/${component}.csv \
          -src_rtl ${sbrdir}/tools/spyglassdft/sbe_wrapper.sv \
          -dst_rtl ${SPYGLASS_RUN_DIR}/sbe_wrapper.sv



   #####################################################
   # Create an empty liblist file, just to make
   # the SPT runSpyglass script happy.
   #####################################################
   cd ${SPYGLASS_RUN_DIR}


   #####################################################
   # Create and point to the waiver file.
   #####################################################
   setenv WAIVER_FILE ${sbrdir}/tools/spyglassdft/iosf_sbc_ep_waivers.swl 


   #####################################################
   # Identify location of the stuckat report
   #####################################################
   setenv STUCKAT_RPT ${sbrdir}/tools/spyglassdft/sbendpoint/${component}/sbe_spyglass_dft/consolidated_reports/sbe_wrapper_dft_dft_scan_ready/stuck_at_coverage.rpt



# Setup for running Spyglass on the router
else 
   if (${dynopt} =~ "nonDyn") then
      set rtltop="${component}"
   else
      set rtltop="${component}_sbr_generic"
   endif

   #####################################################
   # Clean and create the run directory
   #####################################################
   setenv SPYGLASS_RUN_DIR ${sbrdir}/tools/spyglassdft/router/${component}
   if (-d ${SPYGLASS_RUN_DIR}) then
       rm -rf ${SPYGLASS_RUN_DIR}
   endif
   mkdir -p ${SPYGLASS_RUN_DIR}
   cd ${SPYGLASS_RUN_DIR}

   #####################################################
   # Select the input file-list
   #####################################################
   if ($DYN_OPT == "Dyn") then
      setenv RTL_FILE_LIST ${sbrdir}/tools/spyglassdft/sbr_rtl_list_dyn.f
   else
      setenv RTL_FILE_LIST ${sbrdir}/tools/spyglassdft/sbr_rtl_list.f
   endif

   setenv RTL_FILE_LIST ${SPYGLASS_RUN_DIR}/${rtltop}_rtl_list.f



   #####################################################
   # Locate project file
   #####################################################
   setenv SPYGLASS_PROJ_FILE ${SPYGLASS_RUN_DIR}/../${component}_spyglass_dft.prj



   #####################################################
   # Create and point to the waiver file.
   #####################################################
   cp ${sbrdir}/tools/spyglassdft/iosf_sbc_rtr_waivers.swl .
   if ($DYN_OPT == "Dyn") then
      perl -pi -e "s/<inst>/${component}_sbr_generic/" ./iosf_sbc_rtr_waivers.swl
   else
      perl -pi -e "s/<inst>/${component}/" ./iosf_sbc_rtr_waivers.swl
   endif
   setenv WAIVER_FILE ${sbrdir}/tools/spyglassdft/${component}/iosf_sbc_rtr_waivers.swl


   #####################################################
   # Identify location of the stuckat report
   #####################################################
   if ($DYN_OPT == "Dyn") then
      setenv STUCKAT_RPT ${sbrdir}/tools/spyglassdft/router/${component}/${component}_spyglass_dft/consolidated_reports/${component}_sbr_generic_dft_dft_scan_ready/stuck_at_coverage.rpt
   else
      setenv STUCKAT_RPT ${sbrdir}/tools/spyglassdft/router/${component}/${component}_spyglass_dft/consolidated_reports/${component}_dft_dft_scan_ready/stuck_at_coverage.rpt
   endif
   
   set ace_network="-ace_network $config"
endif


${sbrdir}/scripts/qa/get_filelist.pl \
   -run_ace \
   -ace_model cdc_${rtltop} \
   -ace_filter Synthesis \
   -format spyglass \
   -output_file ${RTL_FILE_LIST} \
   $ace_network

set exitStatus=$status
if( ${exitStatus} ) then
   echo "${thisToolName} -E- get_filelist.pl failed."
   exit ${exitStatus}
endif

if ($config == "IOSF_Sideband_Endpoint") then
      echo "${SPYGLASS_RUN_DIR}/sbe_wrapper.sv" >>  $RTL_FILE_LIST
endif 



#####################################################
# Run Spyglass
#####################################################
/p/com/eda/intel/siputils/prod/bin/runGenCmd \
   ../../run.csh \
   -setup none \
   -pc 'SpyGlass Exit Code 0 '
set exitStatus=$status


if ($exitStatus) then
  echo "${thisToolName} -E- Spyglass DFT returned non-zero exit code."
  exit $exitStatus
endif


if ($config == "IOSF_Sideband_Endpoint") then
   echo "${thisToolName} -I- Disabling check on fault coverage for endpoint because it is currently failing."
   echo "                    THIS IS TEMPORARY - the check should be enabled as soon as the SpyglassDFT issues are resolved."
else
   echo "${thisToolName} -I- Checking results in"
   echo "                    ${STUCKAT_RPT}"
   echo $cwd | grep "_misr" >/dev/null
   if($status == 0) then
     #There is no equalent HDK path for checklogfile. Better to copy in local area and use.
      /p/com/eda/intel/siputils/prod/bin/checkLogFile \
            ${STUCKAT_RPT} \
            -st "\<\>TOP MODULE SUMMARY.*" \
            -en "Coverage Summary for each Instance.*" \
            -pc '\s*Fault-coverage\(in \%\)\s+\|\s+[6|7|8|9].*' \
            -pc '\s*Test-coverage\(in \%\)\s+\|\s+[6|7|8|9].*' 
      set exitStatus=$status
   else
      echo $cwd | grep "spt_1p0" >/dev/null
      if($status == 0) then
          /p/com/eda/intel/siputils/prod/bin/checkLogFile \
                ${STUCKAT_RPT} \
                -st "\<\>TOP MODULE SUMMARY.*" \
                -en "Coverage Summary for each Instance.*" \
                -pc '\s*Fault-coverage\(in \%\)\s+\|\s+[8|9].*' \
                -pc '\s*Test-coverage\(in \%\)\s+\|\s+[8|9].*'
          set exitStatus=$status
      else
        echo $SIP_TOOL_VARIATION | grep cnl > /dev/null
        if($status == 0) then
         /p/com/eda/intel/siputils/prod/bin/checkLogFile \
               ${STUCKAT_RPT} \
               -st "\<\>TOP MODULE SUMMARY.*" \
               -en "Coverage Summary for each Instance.*" \
               -pc '\s*Fault-coverage\(in \%\)\s+\|\s+[7|8|9].*' \
               -pc '\s*Test-coverage\(in \%\)\s+\|\s+[7|8|9].*'
            set exitStatus=$status
        else
         /p/com/eda/intel/siputils/prod/bin/checkLogFile \
               ${STUCKAT_RPT} \
               -st "\<\>TOP MODULE SUMMARY.*" \
               -en "Coverage Summary for each Instance.*" \
               -pc '\s*Fault-coverage\(in \%\)\s+\|\s+9.*' \
               -pc '\s*Test-coverage\(in \%\)\s+\|\s+9.*' 
            set exitStatus=$status
        endif
      endif
   endif
endif


if ($exitStatus) then
  echo "${thisToolName} -E- Valid coverage numbers not found in log."
  exit $exitStatus
endif

#####################################################
# Verification
#####################################################
if ($exitStatus) then
  echo "${thisToolName} -E- Spyglass DFT failed."
  exit $exitStatus
else
  echo "${thisToolName} -I- Spyglass DFT ran successfully."
endif

exit 0
